#!/usr/bin/guile \
-e main -s
!#

;; Entry point, parses stdin, command line and call pretty printer
(use-modules (ice-9 getopt-long))
(use-modules (json))

(use-modules ((logipp latex-pp) #:prefix ltx:))

(define version "0.0")

(define (help progname)
  (format #t "\
Usage: ~a [options]
  -m, --uriconv  Path to association list mapping URIs to symbols
  -V, --version  Display version
  -h, --help     Display this help
"
          progname))

(define option-spec
  '((uriconv (single-char #\m) (value #t))
    (version (single-char #\V) (value #f))
    (help    (single-char #\h) (value #f))))

(define (main args)
  (let* ((options (getopt-long args option-spec))
         (uriconv        (option-ref options 'uriconv #f))
         (help-wanted    (option-ref options 'help    #f))
         (version-wanted (option-ref options 'version #f)))
    (if (or help-wanted version-wanted)
        (begin
          (if version-wanted
              (format #t "~a\n" version))
          (if help-wanted
              (help (car args))))
        (if uriconv
            (let* ((ucv-port (open-input-file uriconv))
                   (ucv-alist (read ucv-port)))
              (if (list? ucv-alist)
                  (ltx:pp (json->scm) ucv-alist)
                  (throw 'wrong-uriconv)))
            (ltx:pp (json->scm))))))
